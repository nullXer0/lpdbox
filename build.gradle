defaultTasks 'clean', 'javadoc', 'test', 'check', 'checkstyleMain', 'checkstyleTest', 'assemble'

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:3.0.2'
    }
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'net.researchgate.release'

    version = "1.1"

    check.dependsOn jacocoTestReport

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    checkstyle {
        toolVersion = "6.0"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'
        testImplementation 'org.slf4j:slf4j-simple:1.7.25'
        testImplementation 'junit:junit:4.12'
    }

    checkstyle {
        configFile = new File(rootDir, "config/checkstyle.xml")
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }

    release {
        failOnUnversionedFiles = false
    }

    jar {
        manifest {
            attributes("Specification-Title": "LPD-Server framework",
                 "Implementation-Title": "lpdbox",
                 "Implementation-Version": version,
                 "Implementation-Vendor": "https://github.com/michaelknigge/lpdbox",
                 "Created-By": System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
                 "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
        }
    }

    tasks.register('sourceJar', Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    archiveClassifier = 'sources'
                }

                artifact javadocJar {
                    archiveClassifier = 'javadoc'
                }

                groupId 'de.textmode.lpdbox'
                artifactId 'lpdbox'
                version "${project.version}"
            }
        }
    }
}
